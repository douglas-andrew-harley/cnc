include("config.gcmc");
include("utils.gcmc");

feedmode(FEEDMODE_UPM);
safe_z();

for (n = 0; n < mount_hole_count; n++) {
    comment("Mounting hole ", n);
    mount_cx = hole_0_x + (n * mount_hole_spacing);
    pocket_cylinder(mount_cx, hole_y, mount_hole_radius - bit_radius, bottom_depth);
    if (n != (mount_hole_count - 1)) {
        comment("Screw hole ", n);
        screw_cx = mount_cx + (mount_hole_spacing / 2);
        pocket_cylinder(screw_cx, hole_y, screw_hole_radius - bit_radius, bottom_depth);
    }
}

comment("Lip pocket");
pocket_cuboid(
    /* major_min */       bit_radius,
    /* major_max */       bit_radius + x_total,
    /* minor_min */       bit_radius + lip_y_min + bit_radius,
    /* minor_max */       bit_radius + y_total,
    /* depth */           lip_depth,
    /* vec_major_index */ 0
);

comment("Profile");
profile_polyline(
    {
        [0in,                               0in                                               ],
        [bit_radius + x_total + bit_radius, 0in,                               0.5, tab_length],
        [bit_radius + x_total + bit_radius, bit_radius + y_total + bit_radius, 0.7, tab_length],
        [0in,                               bit_radius + y_total + bit_radius                 ],
        [0in,                               0in,                               0.3, tab_length]
    },
    bottom_depth
);

comment("Safe");
safe_z();
